;顾客订单以均值为30分钟的指数分布到达
;若订单需求数量不能满足，则流失
;每份订单的需求数量与频率关系如下
;0.10	0.25	0.30	0.15	0.12	0.05	0.03
;6	12	18	24	30	36	48
;每销售一件商品可获利50元
;每件商品存储费用为0.05元/分钟
;每次订货费用为2000元
;订货点为400
;订货批量为500
;货物延迟15+-10h到达
;初期库存1000

;仿真30天提供以下信息
;估算总的收益
;每天由于缺货而损失的订单数量分布


ORDER		FUNCTION	RN1,D7
.10,6/.35,12/.60,18/.80,24/.92,30/.97,36/1.0,48
LST		TABLE		X$LST,5,5,10
		INITIAL		X$STK,1000

		GENERATE	(EXPONENTIAL(1,0,30))
		ASSIGN		1,FN$ORDER
		TEST GE		X$STK,P1,OUT
		SAVEVALUE	1+,(P1#50)
		SAVEVALUE	1-,(X$STK#(C1-X$TIME)#0.05)
		SAVEVALUE	TIME,C1
		SAVEVALUE	STK-,P1
		TERMINATE
OUT		SAVEVALUE	LST+,1
		TERMINATE
		
		GENERATE	,,,1
AGN		TEST L		X$STK,400
		ADVANCE	900,600
		SAVEVALUE	1-,(X$STK#(C1-X$TIME)#0.05)
		SAVEVALUE	TIME,C1
		SAVEVALUE	STK+,500
		SAVEVALUE	1-,2000
		TRANSFER	,AGN
		
		GENERATE	1440
		TABULATE	LST
		SAVEVALUE	LST,0
		SAVEVALUE	1-,(X$STK#(C1-X$TIME)#0.05)
		SAVEVALUE	TIME,C1
		TERMINATE	1
		START		30
