;零件以均值为120分钟的指数分布到达
;经过三道工序	每道工序加工一个零件
;工序1	90+-30分钟	整数的离散均匀分布
;工序2	70+-40分钟	整数的离散均匀分布
;工序3	80+-50分钟	整数的离散均匀分布
;最早到期时间优先
;使用表格分析加工一个零件需要的时间分布
;及到期时间内没有完成的不同延迟时间的零件数分布
;预热20个，再仿真1000个

V_DUE		VARIABLE	AC1+P1+P2+P3+130+RN1@101
V_OVER		VARIABLE	AC1-P4
T_TOTAL		TABLE		M1,100,100,20
T_OVER		TABLE		V$V_OVER,50,50,20
		
		GENERATE	(EXPONENTIAL(1,0,120))
		ASSIGN		1,(RN1@61+60)
		ASSIGN		2,(RN1@81+30)
		ASSIGN		3,(RN1@101+30)
		ASSIGN		4,V$V_DUE
		
		LINK		1,P4,WORK1
WORK1		SEIZE		1
		ADVANCE	P1
		RELEASE		1
		UNLINK		1,WORK1,1
		LINK		2,P4,WORK2
WORK2		SEIZE		2
		ADVANCE	P2
		RELEASE		2
		UNLINK		2,WORK2,1
		LINK		3,P4,WORK3
WORK3		SEIZE		3
		ADVANCE	P3
		RELEASE		3
		UNLINK		3,WORK3,1
		TABULATE	T_TOTAL
		TEST G		V$V_OVER,0,ACC			;测试为真则去下一模块，为假则去ACC
		SAVEVALUE	FAI+,1
		TABULATE	T_OVER
		TERMINATE	1
ACC		SAVEVALUE	SUC+,1
		TERMINATE	1
		START		20,NP
		RESET
		START		1000

;LOOP

V_DUE		VARIABLE	AC1+P1+P2+P3+130+RN1@101
V_OVER		VARIABLE	AC1-P4
T_TOTAL		TABLE		M1,100,100,20
T_OVER		TABLE		V$V_OVER,50,50,20
		
		GENERATE	(EXPONENTIAL(1,0,120))
		ASSIGN		1,(RN1@61+60)
		ASSIGN		2,(RN1@81+30)
		ASSIGN		3,(RN1@101+30)
		ASSIGN		4,V$V_DUE
		ASSIGN		5,3
ADD_LP		ASSIGN		WORKNO+,1
		LINK		P$WORKNO,P4,WORK
WORK		SEIZE		P$WORKNO
		ADVANCE	P*WORKNO			;间接地址
		RELEASE		P$WORKNO
		UNLINK		P$WORKNO,WORK,1
		LOOP		5,ADD_LP
		
		TABULATE	T_TOTAL
		TEST G		V$V_OVER,0,ACC			;测试为真则去下一模块，为假则去ACC
		SAVEVALUE	FAI+,1
		TABULATE	T_OVER
		TERMINATE	1
ACC		SAVEVALUE	SUC+,1
		TERMINATE	1
		START		20,NP
		RESET
		START		1000





